amends "../../src/actions.pkl"
import "../../src/trigger.pkl"

local packageName = "pkl-github-actions"

local checkout = new ActionStep {
    name = "Checkout code"
    uses = "actions/checkout@v2"
    with = new Mapping {
        ["fetch-depth"] = 0
    }
}

local updateVersionFile = new ActionStep {
    uses = "DamianReeves/write-file-action@master"
    with = new Mapping {
        ["path"] = ".version"
        ["write-mode"] = "overwrite"
        ["contents"] = "${{ needs.version.outputs.version }}"
    }
}

name = "Build"
on = new trigger.On {
    push = new trigger.Push {
        branches = List(
            "master", "main", "develop"
        )
    }
}

jobs = new Mapping {
    ["version"] = new Job {
        outputs = new Mapping {
            ["current-version"] = "${{ env.CURRENT_VERSION }}"
            ["version"] = "${{ steps.gitversion.outputs.semVer }}"
        }
        steps = List(
            checkout,
            new CommandStep {
                name = "Get current Pkl version"
                run = """
                VER=$(cat .version)
                echo "CURRENT_VERSION=$VER" >> $GITHUB_ENV
                """
            },
            new ActionStep {
                name = "Install gitversion"
                uses = "gittools/actions/gitversion/setup@v0.9.6"
                with = new Mapping {
                    ["versionSpec"] = "5.x"
                }
            },
            new ActionStep {
                id = "gitversion"
                name = "Use gitversion"
                uses = "gittools/actions/gitversion/execute@v0.9.6"
                with = new Mapping {
                    ["useConfigFile"] = true
                    ["configFilePath"] = "./gitversion.yml"
                }
            },
            new CommandStep {
                name = "Display version"
                run = "echo \"SemVer: ${{ steps.gitversion.outputs.semVer }}\""
            }
        )
    }
    ["should-release"] = new Job {
        outputs = new Mapping {
            ["proceed"] = "${{ steps.release.outputs.proceed && github.event_name != 'pull_request' }}"
        }
        steps = List(
            new ActionStep {
                id = "release"
                uses = "phish108/release-check@1.0.15"
                with = new Mapping {
                    ["github-token"] = "${{ secrets.GITHUB_TOKEN }}"
                    ["protected-paths"] = """
                    README.md
                    .version
                    """
                }
            }
        )
    }
    ["release"] = new Job {
        needs = List(
            "version", "should-release"
        )
        permissions = new Mapping {
            ["contents"] = "write"
        }
        `if` = "${{ needs.should-release.outputs.proceed }}"
        steps = List(
            checkout,
            new ActionStep {
                name = "Create Release"
                uses = "actions/create-release@v1"
                env = new Mapping {
                    ["GITHUB_TOKEN"] = "${{ secrets.GITHUB_TOKEN }}"
                }
                with = new Mapping {
                    ["tag_name"] = "v${{ needs.version.outputs.version }}"
                    ["release_name"] = "Release ${{ needs.version.outputs.version }}"
                    ["draft"] = false
                    ["prerelease"] = "${{ github.ref == 'refs/heads/develop' }}"
                }
            },
            new ActionStep {
                name = "Add package specific tag"
                uses = "tvdias/github-tagger@v0.0.1"
                with = new Mapping {
                    ["repo-token"] = "${{ secrets.GITHUB_TOKEN }}"
                    ["tag"] = "\(packageName)@${{ needs.version.outputs.version }}"
                }
            }
        )
    }
    ["version-update"] = new Job {
        needs = List("version", "should-release")
        permissions = new Mapping {
            ["contents"] = "write"
        }
        `if` = "${{ needs.should-release.outputs.proceed }}"
        steps = List(
            checkout,
            updateVersionFile,
            new ActionStep {
                uses = "jacobtomlinson/gha-find-replace@v3"
                with = new Mapping {
                    ["find"] = "${{ needs.version.outputs.current-version }}"
                    ["replace"] = "${{ needs.version.outputs.version }}"
                    ["regex"] = false
                    ["include"] = "README.md"
                }
            },
            new ActionStep {
                uses = "stefanzweifel/git-auto-commit-action@v5"
                with = new Mapping {
                    ["commit_message"] = "Update version in documentation from ${{ needs.version.outputs.current-version }} to ${{ needs.version.outputs.version }}"
                }
            }
        )
    }
    ["upload-project"] = new Job {
        needs = List("version", "release")
        permissions = new Mapping {
            ["contents"] = "write"
        }
        steps = List(
            checkout,
            new ActionStep {
                name = "Install Pkl"
                uses = "pkl-community/setup-pkl@v0"
                with = new Mapping {
                    ["pkl-version"] = "0.25.3"
                }
            },
            updateVersionFile,
            new CommandStep {
                name = "Package Pkl"
                run = "pkl project package src"
            },
            new CommandStep {
                env = new Mapping {
                    ["GITHUB_TOKEN"] = "${{ secrets.GITHUB_TOKEN }}"
                }
                run = """
                TAG=pkl-github-actions@${{ needs.version.outputs.version }}
                gh release upload v${{ needs.version.outputs.version }} .out/$TAG/*
                gh release edit v${{ needs.version.outputs.version }} -t v${{ needs.version.outputs.version }}
                """
            }
        )
    }
}